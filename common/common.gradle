import org.apache.tools.ant.filters.ReplaceTokens

ext {
    ceylonModuleName = 'common'
}

apply from : "${rootProject.projectDir}/gradle/java-for-modules.gradle"
apply plugin : CeylonBuildInfoPlugin

dependencies {
    compile 'org.tautua.markdownpapers:markdownpapers-core:1.3.4'
}

ext {
    generatedSrcDir = "${buildDir}/generated-java"
    versionsTemplate = 'com/redhat/ceylon/common/Versions.java'
}

sourceSets {
    main {
        java {
            exclude versionsTemplate
            srcDir generatedSrcDir
        }
    }
}

task createVersionJava( type : Copy ) {
    group "Build"
    description "Creates Versions.java"
    inputs.property 'revisionInfo', ceylonBuildInfo.revisionInfo
    from 'src/' + versionsTemplate
    into  generatedSrcDir
    filter ReplaceTokens, tokens : [ commit : inputs.properties.revisionInfo ]
    enabled = ceylonBuildInfo.hasGitRepository || ceylonBuildInfo.providedBuildId != null
}

compileJava.dependsOn createVersionJava
